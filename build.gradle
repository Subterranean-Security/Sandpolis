/*******************************************************************************
* SANDPOLIS                                                                    *
*                                                                              *
*******************************************************************************/

buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}

	dependencies {
		classpath ':com.sandpolis.gradle.plugin:'
		classpath ':com.sandpolis.gradle.soi:'
		if (PLUGINS_DEPLOY.toBoolean())
			classpath ':com.sandpolis.gradle.deploy:'
	}
}

plugins {
	id "com.diffplug.gradle.spotless" version "3.24.2"
}

spotless {
	java {
		target '**/*.java'

		eclipse().configFile('gradle/resources/EclipseConventions.xml')
		trimTrailingWhitespace()
		endWithNewline()

		licenseHeaderFile file('gradle/resources/header_java.txt'), "package "
	}
	format 'groovy', {
		target '**/*.groovy'

		trimTrailingWhitespace()
		endWithNewline()

		licenseHeaderFile file('gradle/resources/header_java.txt'), "package "
	}
	format 'gradle', {
		target '**/*.gradle'

		trimTrailingWhitespace()
		endWithNewline()
		indentWithTabs()
	}
	format 'swift', {
		target '**/*.swift'

		trimTrailingWhitespace()
		endWithNewline()
		indentWithTabs()

		licenseHeaderFile file('gradle/resources/header_swift.txt'), "import "
	}
}

if (PLUGINS_DEPLOY.toBoolean())
	apply plugin: 'com.sandpolis.gradle.deploy'

// Apply repository configuration
allprojects {
	repositories {
		mavenCentral()
	}

	buildscript {
		repositories {
			mavenCentral()
		}
	}
}

// Apply metadata configuration
allprojects {
	version = SANDPOLIS_VERSION
}

// Apply testing configuration to subprojects
subprojects {
	plugins.withType(JavaPlugin) {
		test {
			useJUnitPlatform()

			testLogging {
				exceptionFormat = 'full'
			}
		}
	}
}

// A list of projects that will be published to the release repository
def publishProjects = [
	'com.sandpolis.client.mega',
	'com.sandpolis.core.instance',
	'com.sandpolis.core.ipc',
	'com.sandpolis.core.net',
	'com.sandpolis.core.profile',
	'com.sandpolis.core.proto',
	'com.sandpolis.core.soi',
	'com.sandpolis.core.storage.hibernate',
	'com.sandpolis.core.stream',
	'com.sandpolis.core.util',
	'com.sandpolis.core.viewer',
	'com.sandpolis.plugin.desktop',
	'com.sandpolis.plugin.filesys',
	'com.sandpolis.plugin.sysinfo',
	'com.sandpolis.server.vanilla',
	'com.sandpolis.viewer.cli',
	'com.sandpolis.viewer.jfx'
]

// Apply publishing configuration
configure(subprojects.findAll {publishProjects.contains(it.name)}) {
	apply plugin: 'maven-publish'
	apply plugin: 'signing'

	group = 'com.sandpolis'
	archivesBaseName = it.name.replace("com.", "").replaceAll("\\.", "-")

	afterEvaluate { sub ->
		task sourcesJar(type: Jar) {
			from sourceSets.main.allJava
			archiveClassifier = 'sources'
		}

		task javadocJar(type: Jar) {
			from javadoc
			archiveClassifier = 'javadoc'
		}

		publishing {
			publications {
				mavenJava(MavenPublication) {
					groupId = sub.group
					artifactId = sub.archivesBaseName
					version = sub.version

					from sub.components.java
					artifact sourcesJar
					artifact javadocJar
					pom {
						name = sub.name
						description = sub.eclipse.project.comment
						url = 'https://github.com/Subterranean-Security/Sandpolis'
						licenses {
							license {
								name = 'The Apache License, Version 2.0'
								url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							}
						}
						developers {
							developer {
								id = 'cilki'
								name = 'Tyler Cook'
								email = 'tcc@sandpolis.com'
							}
						}
						scm {
							url = 'https://github.com/Subterranean-Security/Sandpolis'
							connection = 'scm:git:git://github.com/Subterranean-Security/Sandpolis.git'
							developerConnection = 'scm:git:ssh://git@github.com/Subterranean-Security/Sandpolis.git'
						}
					}
				}
			}
			repositories {
				maven {
					url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
					credentials {
						username PUBLISH_USER
						password PUBLISH_PASS
					}
				}
			}
		}
		signing {
			sign publishing.publications.mavenJava
		}
	}
}
