//============================================================================//
//                                                                            //
//                Copyright Â© 2015 - 2020 Subterranean Security               //
//                                                                            //
//  This source file is subject to the terms of the Mozilla Public License    //
//  version 2. You may not use this file except in compliance with the MPL    //
//  as published by the Mozilla Foundation at:                                //
//                                                                            //
//    https://mozilla.org/MPL/2.0                                             //
//                                                                            //
//=========================================================S A N D P O L I S==//

plugins {
	id 'eclipse'
	id 'java-library'

	id 'com.sandpolis.gradle.soi'
	id 'com.sandpolis.gradle.deploy'
	id 'org.javamodularity.moduleplugin' version '1.5.0'
}

eclipse {
	project {
		name = 'com.sandpolis.viewer.ascetic'
		comment = 'The CLI viewer instance'
	}
	classpath.file {
		whenMerged {
			entries.findAll { it.kind == 'src' || it.kind == 'lib' }.each { it.entryAttributes['module'] = 'true' }
		}
	}
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.2'

	api project(':module:com.sandpolis.core.instance')
	api project(':module:com.sandpolis.core.ipc')
	api project(':module:com.sandpolis.core.net')
	api project(':module:com.sandpolis.core.proto')
	api project(':module:com.sandpolis.core.soi')
	api project(':module:com.sandpolis.core.util')
	api project(':module:com.sandpolis.core.viewer')

	// https://github.com/qos-ch/logback
	api 'ch.qos.logback:logback-core:1.2.3'
	api 'ch.qos.logback:logback-classic:1.2.3'

	// https://github.com/mabe02/lanterna
	api 'com.googlecode.lanterna:lanterna:3.0.1'

	// https://github.com/netty/netty
	api 'io.netty:netty-common:4.1.43.Final'
	api 'io.netty:netty-codec:4.1.44.Final'
	api 'io.netty:netty-transport:4.1.43.Final'
	api 'io.netty:netty-handler:4.1.43.Final'

	// https://github.com/qos-ch/slf4j
	api 'org.slf4j:slf4j-api:1.7.30'
}

jar {
	manifest {
		attributes(
			'Main-Class': 'com.sandpolis.viewer.ascetic.Main',
			'Class-Path': configurations.runtimeClasspath.collect { 'lib/' + it.getName() }.join(' ')
		)
	}
}

deploy {
	jvmArgs = [
		'-Dlogging.net.traffic.raw=false',
		'-Dlogging.net.traffic.decoded=false',
		'-Dnet.connection.tls=true',
		'-Dplugin.enabled=true',
		'-m com.sandpolis.viewer.ascetic/com.sandpolis.viewer.ascetic.Main'
	]
}
