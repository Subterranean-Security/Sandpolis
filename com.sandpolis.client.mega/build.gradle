/*******************************************************************************
* SANDPOLIS                                                                    *
* com.sandpolis.client.mega                                                    *
*******************************************************************************/

buildscript {
	dependencies {
		classpath ':com.sandpolis.gradle.soi:'
	}
}

plugins {
	id 'eclipse'
	id 'java'

	id 'com.sandpolis.gradle.soi'
	id 'com.sandpolis.gradle.deploy'
}

eclipse {
	project {
		name = 'com.sandpolis.client.mega'
		comment = 'The mega-client instance'
	}
}

configurations {
	internal
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.2'

	compileOnly project(':module:com.sandpolis.core.instance:constants')

	implementation project(':module:com.sandpolis.core.instance')
	implementation project(':module:com.sandpolis.core.ipc')
	implementation project(':module:com.sandpolis.core.net')
	implementation project(':module:com.sandpolis.core.proto')
	implementation project(':module:com.sandpolis.core.soi')
	implementation project(':module:com.sandpolis.core.util')

	// https://github.com/cilki/CompactClassLoader
	internal 'com.github.cilki:compact-classloader:1.2.3'

	// https://github.com/google/guava
	implementation 'com.google.guava:guava:28.1-jre'

	// https://github.com/netty/netty
	implementation 'io.netty:netty-common:4.1.41.Final'
	implementation 'io.netty:netty-codec:4.1.41.Final'
	implementation 'io.netty:netty-transport:4.1.41.Final'
	implementation 'io.netty:netty-handler:4.1.41.Final'

	implementation 'javax.persistence:javax.persistence-api:2.2'

	// https://github.com/qos-ch/slf4j
	implementation 'org.slf4j:slf4j-api:1.7.28'

	// https://github.com/pf4j/pf4j
	implementation 'org.pf4j:pf4j:3.1.0'
}

jar {
	manifest {
		attributes(
			'Main-Class': 'com.github.cilki.compact.BootProxy',
			'Boot-Class': 'com.sandpolis.client.mega.Main'
		)
	}

	from configurations.internal.collect {zipTree(it)}
}

deploy {
	jvmArgs = [
		'-Djava.system.class.loader=com.github.cilki.compact.CompactClassLoader',
		'-Dlogging.net.traffic.raw=false',
		'-Dlogging.net.traffic.decoded=false',
		'-Dnet.connection.tls=true',
		'-Dplugin.enabled=true'
	]
}
