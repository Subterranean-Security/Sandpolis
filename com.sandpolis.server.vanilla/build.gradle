//============================================================================//
//                                                                            //
//                Copyright Â© 2015 - 2020 Subterranean Security               //
//                                                                            //
//  This source file is subject to the terms of the Mozilla Public License    //
//  version 2. You may not use this file except in compliance with the MPL    //
//  as published by the Mozilla Foundation at:                                //
//                                                                            //
//    https://mozilla.org/MPL/2.0                                             //
//                                                                            //
//=========================================================S A N D P O L I S==//

plugins {
	id 'eclipse'
	id 'java-library'

	id 'com.sandpolis.gradle.soi'
	id 'com.sandpolis.gradle.deploy'
	id 'com.bmuschko.docker-java-application' version '6.1.1'
	id 'org.javamodularity.moduleplugin' version '1.5.0'
}

eclipse {
	project {
		name = 'com.sandpolis.server.vanilla'
		comment = 'The server instance'
	}
	classpath.file {
		whenMerged {
			entries.findAll { it.kind == 'src' || it.kind == 'lib' }.each { it.entryAttributes['module'] = 'true' }
		}
	}
}

dependencies {
	testImplementation project(path: ':module:com.sandpolis.core.net', configuration: 'tests')
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.2'

	implementation project(':com.sandpolis.client.mega')
	api project(':module:com.sandpolis.core.instance')
	api project(':module:com.sandpolis.core.ipc')
	api project(':module:com.sandpolis.core.net')
	api project(':module:com.sandpolis.core.profile')
	api project(':module:com.sandpolis.core.proto')
	api project(':module:com.sandpolis.core.soi')
	api project(':module:com.sandpolis.core.storage.hibernate')
	api project(':module:com.sandpolis.core.util')

	// Core plugins
	api project(':plugin:com.sandpolis.plugin.desktop')
	api project(':plugin:com.sandpolis.plugin.shell')
	api project(':plugin:com.sandpolis.plugin.sysinfo')
	api project(':plugin:com.sandpolis.plugin.filesys')

	// https://github.com/FasterXML/jackson-databind
	api 'com.fasterxml.jackson.core:jackson-databind:2.10.1'

	// https://github.com/google/guava
	api 'com.google.guava:guava:28.2-jre'

	// https://github.com/h2database/h2database
	api 'com.h2database:h2:1.4.200'

	// https://github.com/netty/netty
	api 'io.netty:netty-codec:4.1.45.Final'
	api 'io.netty:netty-common:4.1.45.Final'
	api 'io.netty:netty-handler:4.1.45.Final'
	api 'io.netty:netty-transport:4.1.45.Final'

	api 'javax.persistence:javax.persistence-api:2.2'

	// https://github.com/hibernate/hibernate-orm
	api 'org.hibernate:hibernate-core:5.4.11.Final'

	// https://github.com/qos-ch/slf4j
	api 'org.slf4j:slf4j-api:1.7.30'

	// https://github.com/cilki/zipset
	api 'com.github.cilki:zipset:1.2.1'
}

project.evaluationDependsOn(":com.sandpolis.client.installer:jar")
project.evaluationDependsOn(":com.sandpolis.client.installer:py")

jar {
	from (project(':com.sandpolis.client.installer:jar').tasks.getByName('jar').outputs) {
		into "lib"
	}
	from (project(':com.sandpolis.client.installer:py').file('src/sandpolis-client-installer.py')) {
		into "lib"
	}
}

docker {
	javaApplication {
		baseImage = 'openjdk:13.0.1-slim'
		maintainer = 'tcc@sandpolis.com'
		images = ["sandpolis/sandpolis-server:${project.version}"]
		ports = [8768]
		mainClassName = 'com.sandpolis.server.vanilla.Main'
		jvmArgs = [
			'-Dpath.lib=/app/libs',
			'-Dpath.gen=/tmp',
			'-Dserver.debug_client=false',
			'-Dnet.ipc.mutex=false'
		]
	}
}

deploy {
	jvmArgs = [
		'-Dlogging.net.traffic.raw=false',
		'-Dlogging.net.traffic.decoded=false',
		'-Dnet.connection.tls=true',
		'-Dplugin.enabled=true',
		'-m com.sandpolis.server.vanilla/com.sandpolis.server.vanilla.Main'
	]
}
