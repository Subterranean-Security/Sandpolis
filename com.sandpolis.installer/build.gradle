/*******************************************************************************
* SANDPOLIS                                                                    *
* com.sandpolis.installer                                                      *
*******************************************************************************/

plugins {
	id 'eclipse'
	id 'java-library'

	id 'org.openjfx.javafxplugin' version '0.0.8'
	id "com.github.breadmoirai.github-release" version "2.2.9"
	id 'edu.sc.seis.launch4j' version '2.4.6'
}

javafx {
	version = '12'
	modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.graphics' ]
	configuration = 'compileOnly'
}

eclipse {
	project {
		name = 'com.sandpolis.installer'
		comment = 'The installer instance'
	}
}

configurations {
	internal
	installTargets
	windows
	linux
	mac

	windows.extendsFrom(runtimeClasspath)
	linux.extendsFrom(runtimeClasspath)
	mac.extendsFrom(runtimeClasspath)
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.2'

	api project(':module:com.sandpolis.core.instance')
	api project(':module:com.sandpolis.core.ipc')
	api project(':module:com.sandpolis.core.proto')
	api project(':module:com.sandpolis.core.soi')
	api project(':module:com.sandpolis.core.util')

	// https://github.com/cilki/CompactClassLoader
	internal 'com.github.cilki:compact-classloader:1.3.0'

	// https://github.com/google/guava
	api 'com.google.guava:guava:28.1-jre'

	// https://github.com/nayuki/QR-Code-generator
	api 'io.nayuki:qrcodegen:1.5.0'

	// https://github.com/qos-ch/slf4j
	api 'org.slf4j:slf4j-api:1.7.28'

	// Installation targets
	installTargets project(':com.sandpolis.server.vanilla')
	installTargets project(':com.sandpolis.viewer.jfx')
	installTargets project(':com.sandpolis.viewer.cli')
	installTargets project(':com.sandpolis.client.mega')

	// JavaFX runtimes
	windows 'org.openjfx:javafx-base:12.0.2:win'
	windows 'org.openjfx:javafx-fxml:12.0.2:win'
	windows 'org.openjfx:javafx-graphics:12.0.2:win'
	windows 'org.openjfx:javafx-controls:12.0.2:win'

	linux 'org.openjfx:javafx-base:12.0.2:linux'
	linux 'org.openjfx:javafx-fxml:12.0.2:linux'
	linux 'org.openjfx:javafx-graphics:12.0.2:linux'
	linux 'org.openjfx:javafx-controls:12.0.2:linux'

	mac 'org.openjfx:javafx-base:12.0.2:mac'
	mac 'org.openjfx:javafx-fxml:12.0.2:mac'
	mac 'org.openjfx:javafx-graphics:12.0.2:mac'
	mac 'org.openjfx:javafx-controls:12.0.2:mac'
}

jar {
	from configurations.internal.collect {zipTree(it)}
}

task jarOnlineWin(type: Jar, dependsOn: jar) {
	baseName = "SandpolisInstaller-win"
	manifest {
		attributes(
			'Main-Class': 'com.github.cilki.compact.BootProxy',
			'Boot-Class': 'com.sandpolis.installer.Main',
			'Compact-Class-Path': configurations.windows
				.collect { it.getName() }
				.join(' ')
		)
	}

	from zipTree(jar.archivePath)
	from configurations.windows
}

task jarOnlineLin(type: Jar, dependsOn: jar) {
	baseName = "SandpolisInstaller-linux"
	manifest {
		attributes(
			'Main-Class': 'com.github.cilki.compact.BootProxy',
			'Boot-Class': 'com.sandpolis.installer.Main',
			'Compact-Class-Path': configurations.linux
				.collect { it.getName() }
				.join(' ')
		)
	}

	from zipTree(jar.archivePath)
	from configurations.linux
}

task jarOnlineMac(type: Jar, dependsOn: jar) {
	baseName = "SandpolisInstaller-mac"
	manifest {
		attributes(
			'Main-Class': 'com.github.cilki.compact.BootProxy',
			'Boot-Class': 'com.sandpolis.installer.Main',
			'Compact-Class-Path': configurations.mac
				.collect { it.getName() }
				.join(' ')
		)
	}

	from zipTree(jar.archivePath)
	from configurations.mac
}

task jarOffline(type: Jar, dependsOn: jar) {
	baseName = "SandpolisInstaller-offline"
	manifest {
		attributes(
			'Main-Class': 'com.github.cilki.compact.BootProxy',
			'Boot-Class': 'com.sandpolis.installer.Main',
			'Compact-Class-Path': configurations.linux.plus(configurations.windows).plus(configurations.mac)
				.collect { it.getName() }
				.join(' ')
		)
	}

	from zipTree(jar.archivePath)
	from configurations.installTargets

	from configurations.linux.plus(configurations.windows).plus(configurations.mac)
}

artifacts {
	archives jarOnlineWin
	archives jarOnlineLin
	archives jarOnlineMac
	archives jarOffline
}

launch4j {
	outfile = "SandpolisInstaller-win-${project.version}.exe"
	mainClassName = 'com.github.cilki.compact.BootProxy'
	headerType = 'gui'
	stayAlive = 'true'
	companyName = 'Subterranean Security'
	productName = 'Sandpolis Installer'
	windowTitle = 'Sandpolis Installer'
	version = "0.${SANDPOLIS_VERSION}"
	textVersion = "0.${SANDPOLIS_VERSION}"
	jar = jarOnlineWin.archivePath
	copyConfigurable = jarOnlineWin
	icon = "${projectDir}/sandpolis.ico"
}
tasks.createExe.dependsOn(jarOnlineWin)

githubRelease {
	token = GITHUB_TOKEN
	owner = 'Subterranean-Security'
	repo = 'Sandpolis'
	draft = true
	prerelease = true
	releaseAssets = {[
		project.file("build/launch4j/${launch4j.outfile}"),
		jarOnlineLin.archivePath,
		jarOnlineMac.archivePath,
		jarOffline.archivePath
	]}
}
tasks.githubRelease.dependsOn(assemble)
tasks.githubRelease.dependsOn(createExe)
