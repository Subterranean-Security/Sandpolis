//============================================================================//
//                                                                            //
//                Copyright Â© 2015 - 2020 Subterranean Security               //
//                                                                            //
//  This source file is subject to the terms of the Mozilla Public License    //
//  version 2. You may not use this file except in compliance with the MPL    //
//  as published by the Mozilla Foundation at:                                //
//                                                                            //
//    https://mozilla.org/MPL/2.0                                             //
//                                                                            //
//=========================================================S A N D P O L I S==//

plugins {
	id 'eclipse'
	id 'java-library'

	id 'com.sandpolis.gradle.soi'
	id 'com.sandpolis.gradle.plugin'
	id 'com.google.protobuf' version '0.8.11'
	id 'org.javamodularity.moduleplugin' version '1.5.0'
}

eclipse {
	project {
		name = 'com.sandpolis.plugin.shell'
		comment = 'The shell plugin'
	}
	classpath.file {
		whenMerged {
			entries.findAll { it.kind == 'src' || it.kind == 'lib' }.each { it.entryAttributes['module'] = 'true' }
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs 'src/main/proto'
			srcDirs 'gen/main/java'
		}
	}
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.1'

	implementation project(':module:com.sandpolis.core.instance')

	// https://github.com/protocolbuffers/protobuf
	implementation 'com.google.protobuf:protobuf-java:3.11.4'

	// https://github.com/qos-ch/slf4j
	implementation 'org.slf4j:slf4j-api:1.7.30'
}

sandpolis_plugin {
	id = 'com.sandpolis.plugin.shell'
	coordinate = 'com.sandpolis:sandpolis-plugin-shell'
	name = 'Shell Plugin'
	description = 'Shell Plugin'
}

protobuf {
	protoc {
		artifact = 'com.google.protobuf:protoc:3.11.4'
	}

	generatedFilesBaseDir = "$projectDir/gen/"

	clean {
		delete generatedFilesBaseDir
	}
}
